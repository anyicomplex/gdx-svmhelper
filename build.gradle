allprojects {
    apply plugin: "java-library"
    group 'io.github.anyicomplex'
    sourceCompatibility = 1.8
    ext {
        gdxVersion = '1.10.0'
        versionSuffix = '-beta4'
        gdxControllersVersion = '2.2.1'
        graalvmVersion = '22.0.0'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        api "org.graalvm.nativeimage:svm:$graalvmVersion"
    }
}

version gdxVersion + versionSuffix
dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
}

subprojects {
    dependencies {
        implementation project(":")
    }
}

project(":backends:lwjgl3") {
    version gdxVersion + versionSuffix
    dependencies {
        // Compile GraalVM platform library
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-linux-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-windows-amd64:$graalvmVersion"
        compileOnly "org.graalvm.nativeimage:svm-hosted-native-darwin-amd64:$graalvmVersion"
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    }
}

project(":extensions:box2d") {
    version gdxVersion + versionSuffix
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    }
}

project(":extensions:bullet") {
    version gdxVersion + versionSuffix
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
    }
}

project(":extensions:freetype") {
    version gdxVersion + versionSuffix
    dependencies {
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
    }
}

project(":extensions:controllers-lwjgl3") {
    version gdxControllersVersion + versionSuffix
    dependencies {
        implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
    }
}

project(":extensions:controllers-moe") {
    version gdxControllersVersion + versionSuffix
    dependencies {
        //Dependencies coming
    }
}

configure([
        project(":"),
        project(":backends:lwjgl3"),
        project(":backends:moe"),
        project(":extensions:box2d"),
        project(":extensions:bullet"),
        project(":extensions:freetype"),
        project(":extensions:controllers-lwjgl3"),
        //project(":extensions:controllers-moe"),

]) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                def idStr = rootProject.name
                if (project.name != rootProject.name) {
                    idStr += "-"
                    idStr += project.parent.name.substring(0, project.parent.name.length() - 1)
                    idStr += "-"
                    idStr += project.name
                }
                artifactId = idStr
                groupId = project.group
                version = project.version
                from components.java
                pom {
                    def nameStr = "libGDX GraalVM Native Image Helper"
                    if (project.name != rootProject.name) {
                        nameStr += " ("
                        nameStr += project.parent.name.substring(0, 1).toUpperCase()
                        nameStr += project.parent.name.substring(1, project.parent.name.length() - 1)
                        nameStr += " "
                        nameStr += project.name.substring(0, 1).toUpperCase()
                        nameStr += project.name.substring(1, project.name.length())
                        nameStr += ")"
                    }
                    name = nameStr
                    description = 'Experimental helper for libGDX to build GraalVM Native Image.'
                    url = 'https://github.com/anyicomplex/gdx-svmhelper'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'anyicomplex'
                            name = 'Yi An'
                            email = 'anyicomplex@protonmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/anyicomplex/gdx-svmhelper.git'
                        developerConnection = 'scm:git:git@github.com:anyicomplex/gdx-svmhelper.git'
                        url = 'https://github.com/anyicomplex/gdx-svmhelper/tree/main'
                    }
                }
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
        repositories {
            maven {
                name = "OSSRH"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
                if (project.version.endsWith("-SNAPSHOT")) {
                    url "https://s01.oss.sonatype.org/content/repositories/snapshots"
                } else {
                    url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
                }
            }
        }
    }

    signing {
        def key = System.getenv("SIGNING_KEY")
        def password = System.getenv("SIGNING_PASSWORD")
        useInMemoryPgpKeys(key, password)
        sign publishing.publications.mavenJava
    }

}

